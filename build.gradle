buildscript {
    repositories {
        google()
        jcenter()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:4.1.1'
    }
}
allprojects {
    repositories {
        google()
        jcenter()
    }
}

ext.versionCodes = ['armeabi-v7a': 2, 'arm64-v8a': 3, 'x86': 8, 'x86_64': 9]

import com.android.build.OutputFile

apply plugin: 'com.android.application'

android {
    compileSdkVersion 30
    buildToolsVersion "29.0.3"

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 30
        versionCode 211
        versionName "2.1.1"
        archivesBaseName = "GobandroidAIGnuGo-$versionName"
    }

    sourceSets.main {
        jniLibs.srcDir 'src/main/libs'
        jni.srcDirs = [] //disable automatic ndk-build call
    }

    splits {
        abi {
            enable true // enable ABI split feature to create one APK per ABI
            universalApk false //generate an additional APK that targets all the ABIs
            reset()
            include "armeabi-v7a", "arm64-v8a", "x86", "x86_64"
        }
    }

    android.applicationVariants.all { variant ->
        variant.outputs.each { output ->
            output.versionCodeOverride =
                    project.ext.versionCodes.get(output.getFilter(OutputFile.ABI), 0) * 1000000 + android.defaultConfig.versionCode
        }
    }

    /*
    // call regular ndk-build(.cmd)ol script from app directory
    task ndkBuild(type: Exec) {
        commandLine 'ndk-build', '-C', file('src/main').absolutePath
    }


    tasks.withType(JavaCompile) {
        compileTask -> compileTask.dependsOn ndkBuild
    }
    */
}
