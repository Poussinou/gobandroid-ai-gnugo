buildscript {
    repositories {
        google()
        jcenter()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:4.1.1'
    }
}
allprojects {
    repositories {
        google()
        jcenter()
    }
}

ext.versionCodes = ['armeabi-v7a': 2, 'arm64-v8a': 3, 'x86': 8, 'x86_64': 9]

import com.android.build.OutputFile

apply plugin: 'com.android.application'

android {
    compileSdkVersion 30
    buildToolsVersion '30.0.2'
    ndkVersion '21.3.6528147'

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 30
        versionCode 212
        versionName "2.1.2"
        archivesBaseName = "GobandroidAIGnuGo-$versionName"
        externalNativeBuild {
            ndkBuild {
                cFlags ("-Wno-constant-conversion")
            }
        }
    }

    sourceSets.main {
        jniLibs.srcDir 'src/main/libs'
        jni.srcDirs = [] //disable automatic ndk-build call
    }

    splits {
        abi {
            enable true // enable ABI split feature to create one APK per ABI
            universalApk false //generate an additional APK that targets all the ABIs
            reset()
            include "armeabi-v7a", "arm64-v8a", "x86", "x86_64"
        }
    }

    android.applicationVariants.all { variant ->
        variant.outputs.each { output ->
            output.versionCodeOverride =
                    project.ext.versionCodes.get(output.getFilter(OutputFile.ABI), 0) * 1000000 + android.defaultConfig.versionCode
        }
    }

    externalNativeBuild {
        ndkBuild {
            path file('src/main/jni/project/Android.mk')
        }
    }

}
